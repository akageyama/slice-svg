module slisvg_sketch_m
  use slisvg_m
  use slisvg_color_m
  use slisvg_svglib_m
  implicit none
  private

  type, public :: slisvg_sketch__sim_pos_t
    real :: u, v
  end type slisvg_sketch__sim_pos_t

  type :: screen_pos_t
    real :: x, y
  end type screen_pos_t

  type screen_size_t
    real :: width, height
    real :: margin_x, margin_y
  end type screen_size_t

  type, public :: slisvg_sketch_t
    type(screen_size_t) :: screen_size
    real :: u_max, u_min, v_max, v_min
    real :: dxdu, dydv
    char(len=SLISVG__STRLEN_MAX) :: title
    char(len=SLISVG__STRLEN_MAX) :: filename
    type(slisvg_svglib_t) :: slisvg_svglib
  contains
    procedure, private :: initialize_short_args
    procedure, private :: initialize_long_args
    generic :: initialize => initialize_short_args,  &
                             initialize_long_args  
    procedure ::      finalize => slisvg_sketch__finalize
    procedure :: to_screen_pos => slisvg_sketch__to_screen_pos
    procedure ::    group_push => slisvg_sketch__group_push
    procedure ::     group_pop => slisvg_sketch__group_pop
    procedure ::          text => slisvg_sketch__text
    procedure ::       polygon => slisvg_sketch__polygon
    procedure ::          path => slisvg_sketch__path
    procedure ::     rectangle => slisvg_sketch__rectangle
    procedure ::         arrow => slisvg_sketch__arrow
    ! line
    procedure, private :: line_r4
    procedure, private :: line_pos2
    generic :: line => line_r4, line_pos2
  end type slisvg_sketch_t


contains


  subroutine line_r4( sketch, u1, v1,  &
                              u2, v2,  &
                              line_width_in_pixel,   &
                              color,   &
                              no_draw )
    class(slisvg_sketch_t) <in> :: sketch
    real <in> :: u1, v1 ! line start in simulation coords.
    real <in> :: u2, v2 ! line finish in simulation coords.
    real <optin> :: line_width_in_pixel
    type(slisvg_color_t) <optin> :: color
    logical <optin> :: no_draw

    char(len=7) :: color_code

    type(screen_pos_t) :: start, finish

    start  = sketch.to_screen_pos( slisvg_sketch__sim_pos_t(u1,v1) )
    finish = sketch.to_screen_pos( slisvg_sketch__sim_pos_t(u2,v2) )

    color_code = ''  ! Initialization. Will be used below.

    if ( present(no_draw) ) then
      if ( no_draw ) then
        color_code = 'none'
      end if
    else if ( present(color) ) then
      color_code = color.to_code()
    end if

    if ( color_code /= '' ) then
      call sketch.slisvg_svglib.line( start.x,  start.y,         &
                                      finish.x, finish.y,        &
                                      color=color_code,          &
                                      width=line_width_in_pixel )
    else 
      call sketch.slisvg_svglib.line( start.x,  start.y,         &
                                      finish.x, finish.y,        &
                                      width=line_width_in_pixel )
    end if

  end subroutine line_r4


  subroutine line_pos2( sketch, pos1,    &
                                pos2,    &
                                line_width_in_pixel,   &
                                color,   &
                                no_draw )
    class(slisvg_sketch_t) <in> :: sketch
    type(slisvg_sketch__sim_pos_t) <in> :: pos1 ! line start in simulation coords.
    type(slisvg_sketch__sim_pos_t) <in> :: pos2 ! line finish in simulation coords.
    real <optin> :: line_width_in_pixel
    type(slisvg_color_t) <optin> :: color
    logical <optin> :: no_draw

    call line_r4( sketch, pos1.u, pos1.v,                           &
                          pos2.u, pos2.v,                           &
                          line_width_in_pixel=line_width_in_pixel,  &
                          color=color,                              &
                          no_draw=no_draw)

  end subroutine line_pos2


  subroutine slisvg_sketch__arrow( sketch, u,                &
                                       v,                &
                                       vect_u,           &
                                       vect_v,           &
                                       arrow_template )
    class(slisvg_sketch_t) <io> :: sketch
    real <in> :: u, v   ! position of the arrow in sim coords
    real <in> :: vect_u, vect_v  ! vector components in sim 

    type(screen_pos_t) screen_pos, screen_vec
    real :: vect_amp, factor
    real <const> :: TOO_SMALL = 1.e-10  ! a tiny value.
    char(len=*) <optin> :: arrow_template

    vect_amp = sqrt( vect_u**2 + vect_v**2 )
    if ( vect_amp < TOO_SMALL ) return

    factor = slisvg_svglib__get_unit_arrow_length_in_pixel()
    screen_pos = sketch.to_screen_pos( slisvg_sketch__sim_pos_t(u,v) )
    screen_vec.x =  vect_u * factor
    screen_vec.y = -vect_v * factor
    call sketch.slisvg_svglib.arrow( screen_pos.x,    &
                                     screen_pos.y,    &
                                     screen_vec.x,    &
                                     screen_vec.y,    &
                                     arrow_template )
  end subroutine slisvg_sketch__arrow


  subroutine slisvg_sketch__group_push( sketch, id,           &
                                        visibility,           &
                                        line_type,            &
                                        line_width_in_pixel,  &
                                        line_color,           &
                                        line_opacity,         &
                                        fill_color,           &
                                        fill_opacity )
    class(slisvg_sketch_t) <io> :: sketch
    char(len=*) <optin> :: id
    char(len=*) <optin> :: visibility ! 'visible' or 'hidden'
    char(len=6) <optin> :: line_type  ! 'dotted' or
                                      ! 'dashed'
    real <optin> :: line_width_in_pixel
    real <optin> :: line_opacity
    type(slisvg_color_t) <optin> :: line_color
    type(slisvg_color_t) <optin> :: fill_color
    real <optin> :: fill_opacity

    integer :: nop ! number of options
    integer <const> :: MAX_NOP = 20
    char(len=SLISVG__STRLEN_MAX), dimension(MAX_NOP) :: str
    char(len=SLISVG__STRLEN_MAX*MAX_NOP) :: str_long
    integer :: n

    call iClear_str

    nop = 0

    if ( present(id) ) then
      nop += 1
      str(nop) = slisvg_svglib__text_line( 'id=', id )
    end if

    if ( present(visibility) ) then
      nop += 1
      str(nop) = slisvg_svglib__text_line( 'visibility=', visibility )
    end if

    if ( present(line_color) ) then
      nop += 1
      str(nop) = slisvg_svglib__text_line( 'stroke=', line_color.to_code() )
    end if

    if ( present(fill_color) ) then
      nop += 1
      str(nop) = slisvg_svglib__text_line( 'fill=', fill_color.to_code() )
    end if

    if ( present(line_width_in_pixel) ) then
      nop += 1
      str(nop) = slisvg_svglib__text_line( 'stroke-width=', line_width_in_pixel )
    end if

    if ( present(line_opacity) ) then
      nop += 1
      str(nop) = slisvg_svglib__text_line( 'stroke-opacity=', line_opacity )
    end if    

    if ( present(fill_opacity) ) then
      nop += 1
      str(nop) = slisvg_svglib__text_line( 'fill-opacity=', fill_opacity )
    end if    

    if ( present(line_type) ) then
      nop += 1
      select case (line_type)
        case ('dotted')
          str(nop) = 'stroke-dasharray="5 5"'
        case ('dashed')
          str(nop) = 'stroke-dasharray="10 5"'
        case default
          call slisvg__fatal( '__MODLINE__: case error.' )
      end select
    end if    

    if ( nop > 0 ) then
      str_long = ''
      do n = 1, nop
        str_long = trim(str_long) // ' ' // trim(str(n))
      end do
      call sketch.slisvg_svglib.group_push( trim(adjustl(str_long)) )
    else
      call sketch.slisvg_svglib.group_push( ' ' )
    end if

  contains

    subroutine iClear_str
      nop = 0
      str(:) = ''
    end subroutine iClear_str

  end subroutine slisvg_sketch__group_push


  subroutine slisvg_sketch__group_pop( sketch )
    class(slisvg_sketch_t) <io> :: sketch

    call sketch.slisvg_svglib.group_pop
  end subroutine slisvg_sketch__group_pop


  subroutine slisvg_sketch__text( sketch, u, v,        &
                                  string,              &
                                  font_size_in_pixel,  &
                                  text_anchor,         &
                                  color )

    class(slisvg_sketch_t) <io> :: sketch
    real <in> :: u, v
    char(len=*) <in> :: string
    real <in> :: font_size_in_pixel
    char(len=*) <in> :: text_anchor   ! start/ middle / end
    type(slisvg_color_t) <optin> :: color

    type(screen_pos_t) :: pos
    char(len=7) :: color_code

    call slisvg__assert( text_anchor == 'start'  .or.     &
                         text_anchor == 'middle' .or.     &
                         text_anchor == 'end',            &
                         '__MODLINE__: text_anchor error.' )

    pos = sketch.to_screen_pos( slisvg_sketch__sim_pos_t(u,v) )
   
    if ( present(color) ) then
      color_code = color.to_code()
      call sketch.slisvg_svglib.text( pos.x, pos.y,            &
                                  string,                  &
                                  font_size_in_pixel,      &
                                  color=color_code,        &
                                  text_anchor=text_anchor )
    else
      call sketch.slisvg_svglib.text( pos.x, pos.y,        &
                                  string,              &
                                  font_size_in_pixel,  &
                                  text_anchor=text_anchor )
    end if
  end subroutine slisvg_sketch__text


  function slisvg_sketch__to_screen_pos( sketch, slisvg_sketch_pos ) result(screen_pos)
    class(slisvg_sketch_t) <in> :: sketch
    type(slisvg_sketch__sim_pos_t) <in> :: slisvg_sketch_pos
    type(screen_pos_t) :: screen_pos

    !!>
                 umin   u           umax
             . mx .     .            .    
         .....____._____.____________.____
          my |    .     .            .    |
             |    +$ $ $. $ $ $ $ $ U-R...|...vmax
             |    $     .            $    |  .\
             |    $     .            $    |  . delta_v
             |    $     .            $    |  ./
             |    $.....*............$....|...v
             |    $     .\__(u,v)    $    |
             |    $     .            $    |
             |...L-L $ $.$ $ $ $ $ $ $....|...vmin
             |____._____._________________|
                  .     . 
                  .......
                  delta_u
    !!<
    real :: delta_u, delta_v 
    real :: mx, my   ! margin x and y
    delta_u = slisvg_sketch_pos.u - sketch.u_min
    delta_v = sketch.v_max - slisvg_sketch_pos.v
    mx = sketch.screen_size.margin_x
    my = sketch.screen_size.margin_y
    screen_pos.x = mx + sketch.dxdu*delta_u
    screen_pos.y = my + sketch.dydv*delta_v
  end function slisvg_sketch__to_screen_pos


  subroutine initialize_short_args( sketch,                &
                                    lower_left_in_phys,    &
                                    upper_right_in_phys,   &
                                    screen_width_in_pixel, &
                                    title,                 &
                                    filename,              &
                                    unit_arrow_in_pixel,   &
                                    write_arrow_template )
    class(slisvg_sketch_t) <out> :: sketch
    type(slisvg_sketch__sim_pos_t) <in> :: lower_left_in_phys,  &
                                           upper_right_in_phys
    real <in> :: screen_width_in_pixel
    char(len=*) <in> :: title
    char(len=*) <in> :: filename
    real <optin> :: unit_arrow_in_pixel
    logical <optin> :: write_arrow_template

    real :: lower_left_in_phys_coord_u,  &
            lower_left_in_phys_coord_v,  &
            upper_right_in_phys_coord_u, &
            upper_right_in_phys_coord_v

    lower_left_in_phys_coord_u  = lower_left_in_phys.u
    lower_left_in_phys_coord_v  = lower_left_in_phys.v
    upper_right_in_phys_coord_u = upper_right_in_phys.u
    upper_right_in_phys_coord_v = upper_right_in_phys.v

    call initialize_long_args( sketch,                      &
                               lower_left_in_phys_coord_u,  &
                               lower_left_in_phys_coord_v,  &
                               upper_right_in_phys_coord_u, &
                               upper_right_in_phys_coord_v, &
                               screen_width_in_pixel,       &
                               title,                       &
                               filename,                    &
                               unit_arrow_in_pixel,         &
                               write_arrow_template )

  end subroutine initialize_short_args


  subroutine initialize_long_args( sketch,                      &
                                   lower_left_in_phys_coord_u,  &
                                   lower_left_in_phys_coord_v,  &
                                   upper_right_in_phys_coord_u, &
                                   upper_right_in_phys_coord_v, &
                                   screen_width_in_pixel,       &
                                   title,                       &
                                   filename,                    &
                                   unit_arrow_in_pixel,         &
                                   write_arrow_template )
    class(slisvg_sketch_t) <out> :: sketch
    real <in> :: lower_left_in_phys_coord_u,  &
                 lower_left_in_phys_coord_v,  &
                 upper_right_in_phys_coord_u, &
                 upper_right_in_phys_coord_v
    real <in> :: screen_width_in_pixel
    char(len=*) <in> :: title
    char(len=*) <in> :: filename
    real <optin> :: unit_arrow_in_pixel
    logical <optin> :: write_arrow_template

    real :: draw_area_width_in_phys,   &
            draw_area_height_in_phys
    real :: draw_area_width_in_pixel,  &
            draw_area_height_in_pixel
    real :: aspect_ratio

    sketch.u_max = upper_right_in_phys_coord_u
    sketch.u_min = lower_left_in_phys_coord_u
    sketch.v_max = upper_right_in_phys_coord_v
    sketch.v_min = lower_left_in_phys_coord_v

    call slisvg__assert( sketch.u_max > sketch.u_min .and.  &
                         sketch.v_max > sketch.v_min,       &
                         '__MODLINE__: size error.' )

    sketch.title = title
    sketch.filename = filename

    !!>
        In the following fig, 
           L = lower_left (in meters),
           U = upper_right (in meters),
        are coordinates in the simulation
        data with physical unit, such as meters.

          mx = margin in x (in pixels)
          my = margin in y (in pixels)
              
             | mx |                  | mx |
         --- +----------------------------+---
          my |    |      title       |    |    \
         --- | ---+------------------U--- |     s  
             |    |                  |    |     c h
             |    |                  |    |     r e
             |    |                  |    |     e i
             |    |                  |    |     e h
         --- | ---L------------------+--- |     n g
          my |    |                  |    |    /  t
         --- +----------------------------+---     .
             |                            |         .
             |<------ screen width ------>|    (in pixels)
                      (in pixels)
    !!<

    draw_area_width_in_phys  = sketch.u_max - sketch.u_min
    draw_area_height_in_phys = sketch.v_max - sketch.v_min
    aspect_ratio = draw_area_height_in_phys  &
                 / draw_area_width_in_phys

    sketch.screen_size.width = screen_width_in_pixel
    sketch.screen_size.margin_x = 0.1*screen_width_in_pixel
    sketch.screen_size.margin_y = sketch.screen_size.margin_x 

    ! Keeping the aspect ratio,
    draw_area_width_in_pixel = screen_width_in_pixel     &
                             - 2*sketch.screen_size.margin_x
    draw_area_height_in_pixel = draw_area_width_in_pixel &
                              * aspect_ratio
    sketch.screen_size.height = draw_area_height_in_pixel  &
                              + 2*sketch.screen_size.margin_y

    sketch.dxdu = draw_area_width_in_pixel / draw_area_width_in_phys
    sketch.dydv = sketch.dxdu

    call slisvg__assert( sketch.dxdu > 0.0 .and. &
                         sketch.dydv > 0.0,      &
                         '__MODLINE__: Something is strange.' )

    call sketch.slisvg_svglib.open( sketch.filename,           &
                                    sketch.screen_size.width,  &
                                    sketch.screen_size.height )

    if ( present(unit_arrow_in_pixel) ) then
      call slisvg_svglib__set_unit_arrow_length_in_pixel( unit_arrow_in_pixel )
    end if

    if ( present(write_arrow_template) ) then
      if ( write_arrow_template ) then
        call sketch.slisvg_svglib.arrow_template
      end if
    end if
  end subroutine initialize_long_args


  subroutine slisvg_sketch__finalize( sketch )
    class(slisvg_sketch_t) <in> :: sketch

    call sketch.slisvg_svglib.close
  end subroutine slisvg_sketch__finalize


  subroutine slisvg_sketch__polygon( sketch, nverts,   &
                                     verts_sim,        &
                                     width_in_pixels,  &
                                     line_color,       &
                                     fill_color )
    class(slisvg_sketch_t) <io> :: sketch
    integer <in> :: nverts
    type(slisvg_sketch__sim_pos_t), dimension(nverts) <in> :: verts_sim
    real <optin> :: width_in_pixels
    type(slisvg_color_t) <optin> :: line_color
    type(slisvg_color_t) <optin> :: fill_color

    type(screen_pos_t) :: vert_on_screen
    real, dimension(nverts) :: verts_screen_x,  &
                               verts_screen_y
    char(len=7) :: line_color_code , fill_color_code
    integer :: i

    do i = 1, nverts
      vert_on_screen = sketch.to_screen_pos( verts_sim(i) )
      verts_screen_x(i) = vert_on_screen.x
      verts_screen_y(i) = vert_on_screen.y
    end do

    if ( present(line_color) ) then 
      line_color_code = line_color.to_code()
    end if

    if ( present(fill_color) ) then
      fill_color_code = fill_color.to_code()
    else
      fill_color_code = 'none'
    end if

    if ( present(line_color) ) then
      call sketch.slisvg_svglib.polygon( nverts,                       &
                                         verts_screen_x,               &
                                         verts_screen_y,               &
                                         width_in_pixels,              &
                                         line_color=line_color_code,   &
                                         fill_color=fill_color_code )
    else
      call sketch.slisvg_svglib.polygon( nverts,                     &
                                         verts_screen_x,             &
                                         verts_screen_y,             &
                                         width_in_pixels,            &
                                         fill_color=fill_color_code )
    end if
  end subroutine slisvg_sketch__polygon



  subroutine slisvg_sketch__path( sketch, nverts,   &
                                  path_directive,   &
                                  verts_sim,        &
                                  width_in_pixels,  &
                                  line_color,       &
                                  fill_color,       &
                                  line_dash_array )
    class(slisvg_sketch_t) <io> :: sketch
    integer <in> :: nverts
    type(slisvg_sketch__sim_pos_t), dimension(nverts) <in> :: verts_sim
    character, dimension(nverts) <in> :: path_directive
    real <optin> :: width_in_pixels
    type(slisvg_color_t) <optin> :: line_color
    type(slisvg_color_t) <optin> :: fill_color
    char(len=*) <optin> :: line_dash_array

    type(screen_pos_t) :: vert_on_screen
    real, dimension(nverts) :: verts_screen_x,  &
                               verts_screen_y
    char(len=7) :: line_color_code , fill_color_code
    integer :: i

    do i = 1, nverts
      vert_on_screen = sketch.to_screen_pos( verts_sim(i) )
      verts_screen_x(i) = vert_on_screen.x
      verts_screen_y(i) = vert_on_screen.y
    end do

    if ( present(line_color) ) then 
      line_color_code = line_color.to_code()
      if ( present(fill_color) ) then
        fill_color_code = fill_color.to_code()
        call sketch.slisvg_svglib.path( nverts,                       &
                                        path_directive,               &
                                        verts_screen_x,               &
                                        verts_screen_y,               &
                                        line_width_in_pixels          &
                                          =width_in_pixels,           &
                                        line_color=line_color_code,   &
                                        fill_color=fill_color_code,   &
                                        line_dash_array=line_dash_array )
      else !  No fill_color 
        call sketch.slisvg_svglib.path( nverts,                       &
                                        path_directive,               &
                                        verts_screen_x,               &
                                        verts_screen_y,               &
                                        line_width_in_pixels          &
                                          =width_in_pixels,           &
                                        line_color=line_color_code,   &
                                        fill_color='none',            &
                                        line_dash_array=line_dash_array )
      end if
    else  ! No line_color
      if ( present(fill_color) ) then
        fill_color_code = fill_color.to_code()
        call sketch.slisvg_svglib.path( nverts,                       &
                                        path_directive,               &
                                        verts_screen_x,               &
                                        verts_screen_y,               &
                                        line_width_in_pixels          &
                                          =width_in_pixels,           &
                                        fill_color=fill_color_code,   &
                                        line_dash_array=line_dash_array )
      else !  No fill_color 
        call sketch.slisvg_svglib.path( nverts,                       &
                                        path_directive,               &
                                        verts_screen_x,               &
                                        verts_screen_y,               &
                                        line_width_in_pixels          &
                                          =width_in_pixels,           &
                                        fill_color='none',            &
                                        line_dash_array=line_dash_array )
      end if
    end if

  end subroutine slisvg_sketch__path


  subroutine slisvg_sketch__rectangle( sketch, lower_left,           &
                                               upper_right,          &
                                               line_width_in_pixel,  &
                                               line_color,           &
                                               fill_color  )
    class(slisvg_sketch_t) <io> :: sketch
    type(slisvg_sketch__sim_pos_t) <in> :: lower_left, upper_right
    real <in> :: line_width_in_pixel
    type(slisvg_color_t) <in> :: line_color
    type(slisvg_color_t) <optin> :: fill_color

    type(screen_pos_t) :: screen_ll, screen_ur
    char(len=7) :: line_color_code , fill_color_code
    real, dimension(4) :: screen_verts_x,   &
                          screen_verts_y

    line_color_code = line_color.to_code()

    if ( present(fill_color) ) then
      fill_color_code = fill_color.to_code()
    else
      fill_color_code = 'none'
    end if

    screen_ll  = sketch.to_screen_pos( lower_left )
    screen_ur  = sketch.to_screen_pos( upper_right )

    screen_verts_x(1) = screen_ll.x
    screen_verts_y(1) = screen_ll.y
    screen_verts_x(2) = screen_ur.x
    screen_verts_y(2) = screen_ll.y
    screen_verts_x(3) = screen_ur.x
    screen_verts_y(3) = screen_ur.y
    screen_verts_x(4) = screen_ll.x
    screen_verts_y(4) = screen_ur.y

    call sketch.slisvg_svglib.polygon( 4,                    &
                                       screen_verts_x,       &
                                       screen_verts_y,       &
                                       line_width_in_pixel,  &
                                       line_color_code,      &
                                       fill_color_code )
  end subroutine slisvg_sketch__rectangle

end module slisvg_sketch_m

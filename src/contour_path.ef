module contour_path_m
  use ut_m
  use slice_mesh_m
  implicit none
  private
  !!>
        P: positive edge: eastward for edgex and
                          northward for edgey
        M: negative edge: westward for edgex and
                          southward for edgey 
        1: vertex on which paths run straight forward.
        2: vertex on which paths turn left.
        3: vertex on which paths turn right.

       +---+---+---+---+---2=M=1=M=2
       |   |   |   |   |   M       P
       +---+---+---2=M=1=M=3=P=1=P=2
       |   |   |   M       P   |   |
       2=M=1=M=2---2=P=1=P=2---+---+
       M       P   |   |   |   |   |
       2=P=1=P=2=M=1=M=1=M=1=M=2---+
       |   |   M               P
       +---+---1           3=P=2
       |   |   M           P   |
       +---+---2=P=1=P=1=P=2---+

      To round the corner, we make a 45-degree turn
      at each corner, instead of 90-degree turn.
      In the figure below, we came from left, along the
      bottom line, and have just made a turn to left
      at the corner point "2". And the present vertex
      position (vpos_now) is "1". To draw a "rounded"
      path (...L...L...) with double 45-degree turns.

                        L
                        L
                        1
                      L .\____vpos_now
                    L   .
                  L     .
        L L L L 1 . . . 2
                         \_____vpos_prev
  !!<


  type, public :: contour_path_t
    integer :: vert_nu, vert_nv
    integer :: num ! total number of vertex points of the path
    type(slice_mesh_ivect_t) :: vpos_start
    real, dimension(:), allocatable :: list_u, list_v
  contains
    procedure :: initialize => contour_path__initialize
    procedure :: reset => contour_path__reset
    procedure :: trace_and_erase  &
                       => contour_path__trace_and_erase
    procedure, pass :: set_starting_edge &
                       => contour_path__set_starting_edge
  end type contour_path_t


contains


  subroutine turn( which_way,       &
                   path,            &
                   edge,            &
                   direct_now,      &
                   vpos_now,        &
                   moved_one_step,  &
                   path_is_closed )
    char(len=*) <in> :: which_way
    type(contour_path_t) <in> :: path
    type(slice_mesh__edge_t) <io> :: edge
    type(slice_mesh_direct_t) <io> :: direct_now 
    type(slice_mesh_ivect_t) <io> :: vpos_now
    logical <out> :: moved_one_step
    logical <out> :: path_is_closed
    !!>
               ^      When direct_now = 'East' and the current
               |      vertex position (i,j)=(i_now,j_now), which is
          E--> + --   denoted by '+'. If which_way = 'left', you 
               |      are trying to turn left (northward).
    !!<
    type(slice_mesh_direct_t) :: direct_turned

    moved_one_step = .false.  ! Default.
    path_is_closed = .false.  ! Default.

    call ut__assert( which_way == 'left'    .or.  &
                     which_way == 'right'   .or.  &
                     which_way == 'forward',      &
                    '__MODFUNC__: Bad argument "which_way".' )

    direct_turned = direct_now.turn( which_way )

    if ( edge.can_turn_to( vpos_now, direct_turned ) ) then
      call edge.erase( vpos_now, direct_turned.d )
      vpos_now = vpos_now.shift( direct_turned.d )
      direct_now = direct_turned
      moved_one_step = .true.
      path_is_closed = returned_to_start_point( path.vpos_start, vpos_now )
    end if
  end subroutine turn


  function returned_to_start_point( vpos_start, vpos_now ) result(ans)
    type(slice_mesh_ivect_t) <in> :: vpos_start, vpos_now
    logical :: ans
    ans = ( vpos_start.i == vpos_now.i  .and.  &
            vpos_start.j == vpos_now.j )
  end function returned_to_start_point  


  subroutine contour_path__reset( this )
    class(contour_path_t) <io> :: this
    this.num = 0
    this.list_u(:) = 0.0
    this.list_v(:) = 0.0
  end subroutine contour_path__reset


  subroutine contour_path__initialize( this, vert_nu, vert_nv )
    class(contour_path_t) <out> :: this
    integer <in> :: vert_nu, vert_nv

    integer :: max_path_element_num

    max_path_element_num = vert_nu * vert_nv

    allocate(this.list_u(max_path_element_num))
    allocate(this.list_v(max_path_element_num))

    this.vert_nu = vert_nu
    this.vert_nv = vert_nv
    call contour_path__reset( this )
  end subroutine contour_path__initialize


  subroutine contour_path__set_starting_edge( this,         &
                                              mesh_vert,    &
                                              mesh_edge,    &
                                              direct_now,   &
                                              status,       &
                                              vpos_now )
    class(contour_path_t) <io> :: this
    type(slice_mesh__vert_t) <in> :: mesh_vert
    type(slice_mesh__edge_t) <io> :: mesh_edge
    type(slice_mesh_direct_t) <in> :: direct_now
    char(len=10) <out> :: status ! 'no contour' or 'have found'
    type(slice_mesh_ivect_t) <out> :: vpos_now ! vert position

    type(slice_mesh_ivect_t) :: vpos_start
    !!>
          Tring to find an eastward arrow AB.

         | .    |       .
         |  .   |     . |
         |   .  |   .   |
       --m----.-p=.=====m--
         |      .   \_____ Find this edge.
    !!< 
    integer :: ei, ej
    integer :: quinary
    real :: path_tip_u, path_tip_v

    call ut__assert( direct_now.d==SLICE_MESH__PATH_NORTHWARD,  &
                    '__MODFUNC__: Path must cross the start edge northtward.' )
    call ut__assert( this.num == 0,  &
                    '__MODFUNC__: You forgot to reset contour_path.' )
    !!> 
        Find a starting edge of a contour line for the input level.
        Mathematically, every contour line is closed unless it touches 
        on the boundary of the simulatin box (u=umin/umax or v=vmin/vmax).

        We set a virtual contour line along the boundary if the boudary grid
        vertices satisfy the condition 
            vert_val >= level  (denoted by "p" in the figure),
        see the upper right figure. 

        Since all contour lines are thus always closed, we first find an
        edge (starting edge) for each contour and draw the contour with
        keeping grid vertices with 
            vert_val >= level
        always in its left-hand side of the path.
        
        Starting edge of a trully closed contour (without touching the
        boundaries) is any u_edge with its quinary value "PM" (plus-in-left
        and minus-in-right of the edge).

        Cares should be taken for contour lines with virual (on-boundary)
        contours. 
                            contour line
      [Case A]                *                 
             ei     ei+1    *           [Case B]                ei=1
             |       |    *                               *   (right boundary)
         - - p - - - p -*-                |       |     * |       *
             |       |*                   |       |   *   |       *
             |      *|                  - m - - - m -*- - p - - - B
             |    *  |                    |       |  *    |       *
      ej - - P==*====M - -                |       |  *    |       *
             | *  \  |                    |       |  *    |       *
             |*   PM |                  - m - - - m -*- - p - - - B
             *      \                     |       | *     |       *
                     \                    |       |*      |       *
                   start edge             |      *|       |       *  ej=1    
                                        --m----***B*******B*******B (bottom
      [Case C]                             \     / \     / \     /   boundary) 
       ei=1 (left boundary)                   MP      BB      BB        
        |     * |       |                              \
        |   *   |       |                               start edge
        m -*- - p - - - p -          [Case D]    
        | *     |       |                 |       |      *|       |
        |*      |       |             - - p - - - p - - * m - - - m - -
        *       |       |                 |       |    *  |       |
        B - - - p - - - p -               |       |   *   |       |
        *       |       |                 |       |  *    |       |   ej=1
        *       |       |              ***B*******B** - - m - - - m - (bottom
        *       |       |                  \     / \     / \     /     boundary)
        B*******B*******B*** ej=1             BB      PM      MM
         \     / \     /    (bottom                    \
            BB      BB       boundary)                  start edge
             \
              start edge
    !!< 
    do ej = 1, mesh_edge.u_nv
      do ei = 1, mesh_edge.u_nu
        quinary = mesh_edge.u_quinary(ei,ej)
        if ( quinary == SLICE_MESH__EDGE_LEVEL_QUINARY_PM   &
                    .or.                                    &
             quinary == SLICE_MESH__EDGE_LEVEL_QUINARY_BB ) then
          this.vpos_start = slice_mesh_ivect_t(ei,ej)
          path_tip_v = mesh_vert.pos_v(ej)
          if ( quinary == SLICE_MESH__EDGE_LEVEL_QUINARY_PM ) then
            ! [Case A] and [Case D] in above figure.
            path_tip_u = mesh_edge.u_cross_coord(ei,ej)
            call register( this, path_tip_u, path_tip_v )
          else if ( quinary == SLICE_MESH__EDGE_LEVEL_QUINARY_BB ) then
            ! [Case B] and [Case C] in above figure.
            path_tip_u = mesh_vert.pos_u(ei)    ! left vertex of the edge
            call register( this, path_tip_u, path_tip_v )
            path_tip_u = mesh_vert.pos_u(ei+1)  ! right vertex of the edge
            call register( this, path_tip_u, path_tip_v )
          else
            call ut__fatal( "__MODFUNC__: Missing some case?" )
          end if

          
          vpos_now = vpos_start.shift( SLICE_MESH__EAST )
          call register( this, mesh_vert, vpos_now )
          call mesh_edge.erase( vpos_start, SLICE_MESH__EAST )
          status = 'have found'
          return
        end if
      end do
    end do 
    status = 'no contour'
  end subroutine contour_path__set_starting_edge


  subroutine register( contour_path, u, v )
    type(contour_path_t) <io> :: contour_path
    real <in> :: u, v

    integer :: n

    n = contour_path.num + 1
    call ut__assert( n <= size(contour_path.list_u,dim=1),  &
                    '__MODFUNC__: Increase array size of path_vert.' )

    contour_path.list_u(n) = u
    contour_path.list_v(n) = v
    contour_path.num = n
  end subroutine register


  subroutine contour_path__trace_and_erase( this,       &
                                            mesh_vert,  &
                                            mesh_edge,  &
                                            direct_now, &
                                            vpos_now )
    class(contour_path_t) <io> :: this
    type(slice_mesh__vert_t) <in> :: mesh_vert
    type(slice_mesh__edge_t) <io> :: mesh_edge
    type(slice_mesh_direct_t) <io> :: direct_now  ! E(ast)/N(orth)/W(est)/S(outh)-ward
    type(slice_mesh_ivect_t) <io> :: vpos_now

    logical :: moved_one_step, path_is_closed

    do
        !!> 
          Turn left if you can.
                       C
                       |
                       |
                       |
            A -------> B <= You are here. Checking if you can
                  \         turn to the left (upward) direction.
                   \
                    You came along this edge.
        !!<
      call turn( 'left',          &
                 this,            &
                 mesh_edge,       &
                 direct_now,      &
                 vpos_now,        &
                 moved_one_step,  &
                 path_is_closed )
      if ( moved_one_step ) then
        !!>
            OK. You have turned to left. "vpos" has moved.
            
                         C <= You are here now.
                         |
                         M <= Place a path point at 
                         |    the middle of edge BC.
               A - - - - B
        !!<
        call register( this, mesh_vert, vpos_now )
      end if
      if ( path_is_closed ) return
        ! Your job is done if you have returned to the starting point.

      !!>
        You are at C. Turn right if you can the next step,
        and you will place another path point at the middle
        of edge CD.
        
           O - - - - - - - C - - - M - --> D
           |               |     *         |
           |               |   *           |
           |               | *             |
           |               M(iddle of BC)  |
           |             * |               |
           |           *   |               |
           |         *     |               |
           A - - - * - - - B - - - - - - - O
      !!<
      call turn( 'right',         &
                 this,            &
                 mesh_edge,       &
                 direct_now,      &
                 vpos_now,        &
                 moved_one_step,  &
                 path_is_closed )
      if ( moved_one_step ) then
        call register( this, mesh_vert, vpos_now )
      end if
      if ( path_is_closed ) return

      !!>
         If you could not turn left nor right, you should
         be able to go forward.
      !!<
      call turn( 'forward',       &
                 this,            &
                 mesh_edge,       &
                 direct_now,      &
                 vpos_now,        &
                 moved_one_step,  &
                 path_is_closed )
      if ( moved_one_step ) then
        call register( this, mesh_vert, vpos_now )
      end if
      if ( path_is_closed ) return
    end do

  end subroutine contour_path__trace_and_erase

end module contour_path_m

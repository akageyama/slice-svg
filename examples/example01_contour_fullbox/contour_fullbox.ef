module simulation_region_m
  implicit none
  private

  type, private :: simulation_region_t
    real :: xmin, xmax
    real :: ymin, ymax
  end type simulation_region_t
   
  real <const> :: XMIN_ = -20.0 ! temporal, private
  real <const> :: XMAX_ =  80.0
  real <const> :: YMIN_ = -20.0
  real <const> :: YMAX_ =  30.0

  type(simulation_region_t), public ::  &
    SIMULATION_REGION = simulation_region_t( XMIN_, XMAX_,  &
                                             YMIN_, YMAX_ )
end module simulation_region_m


module grid_m
  use simulation_region_m
  implicit none
  private
  public :: GRID__NX, GRID__NY

  integer <const> :: GRID__NX = 200
  integer <const> :: GRID__NY = 100

  type, public :: grid_t
    real, dimension(GRID__NX) :: pos_x
    real, dimension(GRID__NX) :: pos_y
  contains
    procedure :: initialize => grid__initialize
  end type grid_t

  type(grid_t), public :: Grid

contains

  subroutine grid__initialize( grid )
    class(grid_t) <out> :: grid

    integer :: i, j
    real :: dx, dy

    associate( SREG => SIMULATION_REGION )
      dx = (SREG.xmax-SREG.xmin)/(GRID__NX-1)
      do i = 1, GRID__NX
        grid.pos_x(i) = SREG.xmin + dx*(i-1)
      end do

      dy = (SREG.ymax-SREG.ymin)/(GRID__NY-1)
      do j = 1, GRID__NY
        grid.pos_y(j) = SREG.ymin + dy*(j-1)
      end do
    end associate
  end subroutine grid__initialize

end module grid_m


module temperature_m
  use grid_m
  use simulation_region_m
  implicit none
  private

  type, public :: temperature_t
    real :: vmax, vmin ! max and mini values
    real, dimension(GRID__NX,GRID__NY) :: field2d
  contains
    procedure :: set => temperature__set
  end type temperature_t

contains
 
  subroutine temperature__set( temp )
    class(temperature_t) <out> :: temp

    integer :: i, j
    real :: x, y, x_, y_
    real :: scale
    scale = max(SIMULATION_REGION.xmax,  &
                SIMULATION_REGION.xmin )        

    do j = 1, GRID__NY
      do i = 1, GRID__NX
        x = Grid.pos_x(i)
        y = Grid.pos_y(j)
        x_ = x / scale
        y_ = y / scale
        temp.field2d(i,j) = exp( - (x_**2 + y_** 2) )
      end do
    end do

    temp.vmax = maxval( temp.field2d(:,:) )
    temp.vmin = minval( temp.field2d(:,:) )
  end subroutine temperature__set

end module temperature_m


program main
  use simulation_region_m
  use grid_m
  use temperature_m
  use vv_m
  use vv_color_m
  use vv_sketch_m
  use vv_slice_scalar_m
  implicit none

  type(temperature_t) :: temperature
  real :: level ! contour level
  real :: temp_range, dlevel

  type(vv_sketch_t) :: vv_sketch
  type(vv_slice_scalar_t) :: vv_scalar
  type(vv_color_t) :: color
  integer :: l ! level

  call Grid.initialize
  call temperature.set
  call vv_scalar.initialize( 'temperature',  &
                             GRID__NX,       &
                             GRID__NY,       &
                             Grid.pos_x,     &
                             Grid.pos_y,     &
                             temperature.field2d )
  call iSketch_initialize
  call iSketch_message_header( 'Color contouring with SVG' )
  call iSketch_boundary

  temp_range = temperature.vmax - temperature.vmin
  dlevel = temp_range / 10
  call vv_sketch.group_push( line_color=VV_COLOR__CONST.black )

    do l = 1, 10
      level = temperature.vmin + dlevel*(real(l)-0.5)
      color = vv_color__real_to_color( vmin=temperature.vmin,  &
                                       vmax=temperature.vmax,  &
                                       val=level )
      call vv_scalar.vis_contour( vv_sketch,  &
                                  level,  &
                                  fill_color=color )
    end do

  call vv_sketch.group_pop
  call iSketch_message_footer
  call vv_scalar.finalize
  call iSketch_finalize

contains

  subroutine iSketch_initialize
    call vv_sketch.initialize( SIMULATION_REGION.xmin,  &
                               SIMULATION_REGION.ymin,  &
                               SIMULATION_REGION.xmax,  &
                               SIMULATION_REGION.ymax,  &
                               screen_width_in_pixel=1200.0,  &
                               title='Temperature field',  &
                               filename='example01_contour_fullbox.svg' )
  end subroutine iSketch_initialize

  subroutine iSketch_message_header( string )
    char(len=*) <in> :: string
    call vv_sketch.text( ( SIMULATION_REGION.xmin         &
                         + SIMULATION_REGION.xmax ) / 2,  &
                         SIMULATION_REGION.ymax * 1.1,    &
                         string,  &
                         font_size_in_pixel=30.0,  &
                         text_anchor='middle', &
                         color=VV_COLOR__CONST.black )
  end subroutine iSketch_message_header

  subroutine iSketch_message_footer
    real :: vmin, vmax
    char(len=200) :: string

    real :: text_v_pos_in_physical_unit
    real :: tsep ! text v position separation betwen lines

    tsep = ( SIMULATION_REGION.ymax  &
           - SIMULATION_REGION.ymin ) * 0.06
    text_v_pos_in_physical_unit = SIMULATION_REGION.ymin  &
                                - tsep
    vmin = temperature.vmin
    vmax = temperature.vmax
    string = ' vmin = ' // vv__real_to_str13(vmin) //  &
             ' vmax = ' // vv__real_to_str13(vmax)
    call vv_sketch.text( ( SIMULATION_REGION.xmin         &
                         + SIMULATION_REGION.xmax ) / 2,  &
                         text_v_pos_in_physical_unit,  &
                         string,  &
                         font_size_in_pixel=20.0,  &
                         text_anchor='middle', &
                         color=VV_COLOR__CONST.black )
    text_v_pos_in_physical_unit -= tsep
    string = trim(vv_sketch.filename)
    call vv_sketch.text( ( SIMULATION_REGION.xmin         &
                         + SIMULATION_REGION.xmax ) / 2,  &
                         text_v_pos_in_physical_unit,  &
                         string,  &
                         font_size_in_pixel=20.0,  &
                         text_anchor='middle', &
                         color=VV_COLOR__CONST.black )
  end subroutine iSketch_message_footer

  subroutine iSketch_finalize
    call vv_sketch.finalize                 
  end subroutine iSketch_finalize

  subroutine iSketch_boundary
    call vv_scalar.mesh.draw( vv_sketch, &
                              line_color=VV_COLOR__CONST.black, &
                              width_in_pixels=2.0 )
  end subroutine iSketch_boundary

end program main
